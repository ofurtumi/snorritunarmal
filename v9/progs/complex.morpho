;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Design document
;;; =======================================
;;;
;;; Fastyrðing gagna: Tvinntala með gildið 
;;; x + iy, táknuð með listanum [x, y]
;;;
;;; =======================================
;;;
;;; Notkun:     z = createComp(x,y);
;;; Fyrir:      x og y eru rauntölur
;;; Eftir:      z er listi þar sem haus er 
;;;             raunpartur og hali er ímyndaði partur
;;;
;;; Notkun:     a = getReal(z);
;;; Fyrir:      z er tvinntala
;;; Eftir:      a er raunpartur z        
;;;
;;; Notkun:     b = getImag(z)
;;; Fyrir:      z er tvinntala
;;; Eftir:      b er ímyndaði partur z
;;;
;;; Notkun:     out = printImag(z);
;;; Fyrir:      z er tvinntala
;;; Eftir:      out er gildi strengur með gildum á 
;;;             haus og hala z á lesanlegu formi
;;;
;;; Notkun:     z = a +++ b;
;;; Fyrir:      a og b eru tvinntölur
;;; Eftir:      z er tvinntölusamlagning a og b
;;;
;;; Notkun:     z = a --- b;
;;; Fyrir:      a og b eru tvinntölur
;;; Eftir:      z er tvinntölufrádráttu a og b
;;;
;;; Notkun:     z = a *** b;
;;; Fyrir:      a og b eru tvinntölur
;;; Eftir:      z er tvinntölumargföldun a og b
;;;
;;; Notkun:     z = a /// b;
;;; Fyrir:      a og b eru tvinntölur
;;; Eftir:      z er tvinntöludeiling a og b
 

"complex.mmod" =
!
{{
createComp = 
    fun(x, y)
    {
        return [x, y];
    };

getReal = 
   fun(z)
    {
        return head(z);
    };

getImag = 
    fun(z)
    {
        return head(tail(z));
    };

printImag =
   fun(z)
    {
        val out = "\n\treal: " ++ getReal(z) ++ "  \n\timag: " ++ getImag(z);
        return out;
    };

+++ = 
    fun(a, b)
    {
        return createComp(getReal(a) + getReal(b), getImag(a) + getImag(b));
    };

--- = 
    fun(a, b)
    {
        return createComp(getReal(a)-getReal(b),getImag(a)-getImag(b));
    };

*** = 
    fun(a, b)
    {
        return createComp(
            getReal(a)*getReal(b)-getImag(a)*getImag(b),
            getReal(a)*getImag(b)+getImag(a)*getReal(b)
        );
    };

/// = 
    fun(a, b)
    {
        val temp = getReal(b)*getReal(b)+getImag(b)*getImag(b);
        return createComp(
            (getReal(a)*getReal(b)+getImag(a)*getImag(b))/temp, 
            (getImag(a)*getReal(b)-getReal(a)*getImag(b))/temp
        );
    };
}};

;;; Test forrit fyrir 
"complex.mexe" = main in
{{
main =
	fun()
	{
		try
		{
			val c1 = createComp(10.0, 20.0);
            writeln("c1: \t   " ++ printImag(c1));

			val c2 = createComp(5.0, 9.0);
            writeln("c2: \t   " ++ printImag(c2));

            val temp1 = c1 +++ c2;
            writeln("c1 +++ c2: " ++ printImag(temp1));
            
            val temp2 = c1 --- c2;
            writeln("c1 --- c2: " ++ printImag(temp2));
            
            val temp3 = c1 *** c2;
            writeln("c1 *** c2: " ++ printImag(temp3));
            
            val temp4 = c1 /// c2;
            writeln("c1 /// c2: " ++ printImag(temp4));


		}
		catch(e)
		{
			printExceptionTrace(&e)
		}
	};
}}
*
"complex.mmod"
*
BASIS
;